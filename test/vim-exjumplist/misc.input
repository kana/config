" Test: vim-exjumplist misc
"
" NB: There is no way to reset jumplist easily,
"     so that all tests in this file may have side effects.

runtime! plugin/exjumplist.vim
call vspec#hint({'scope': 'exjumplist#_scope()', 'sid': 'exjumplist#_sid()'})
function s:describe__exjumplist_go_first()  "{{{1
  It requires the following initialization

  silent help usr_03.txt
  normal! 10G
  normal! 100G

  let [o0, n0] = Call('s:jumplist_info')
  Should o0 >= 3
  Should n0 == 0
  Should o0 + n0 >= 3

  It should go to the first position in jumplist

  silent call exjumplist#go_first()
  let [o1, n1] = Call('s:jumplist_info')
  Should o1 == 0
  Should n1 >= 3
  Should n1 == o0
endfunction




function s:describe__exjumplist_go_last()  "{{{1
  It requires the following initialization

  silent help usr_04.txt
  normal! 10G
  normal! 100G
  silent call exjumplist#go_first()

  let [o0, n0] = Call('s:jumplist_info')
  Should o0 == 0
  Should n0 >= 3
  Should o0 + n0 >= 3

  It should go to the last position in jumplist

  silent call exjumplist#go_last()
  let [o1, n1] = Call('s:jumplist_info')
  Should o1 >= 3
  Should n1 == 0
  Should o1 == n0
endfunction




function s:describe__exjumplist_next_buffer__at_last_buffer()  "{{{1
  It requires the following initialization

  silent help usr_05.txt
  silent help usr_06.txt
  silent help usr_07.txt
  normal! 10G
  execute 'normal!' "\<C-o>"

  let [o0, n0] = Call('s:jumplist_info')
  let bufnr0 = bufnr('')
  Should o0 >= 3
  Should n0 == 1
  Should o0 + n0 >= 4

  It should does nothing at the last buffer

  silent call exjumplist#next_buffer()
  let [o1, n1] = Call('s:jumplist_info')
  let bufnr1 = bufnr('')
  Should o1 == o0
  Should n1 == n0
  Should bufnr1 == bufnr0
endfunction




function s:describe__exjumplist_next_buffer__at_middle()  "{{{1
  It requires the following initialization

  silent help usr_05.txt
  let bufnr5 = bufnr('')
  normal! 5G
  silent help usr_06.txt
  let bufnr6 = bufnr('')
  normal! 6G
  silent help usr_07.txt
  let bufnr7 = bufnr('')
  normal! 7G
  silent execute 'normal!' "4\<C-o>"

  let [o0, n0] = Call('s:jumplist_info')
  Should o0 >= 1
  Should n0 == 4
  Should o0 + n0 >= 5
  Should bufnr('') == bufnr5

  It should go to the next buffer - usr_06.txt

  silent call exjumplist#next_buffer()
  let [o1, n1] = Call('s:jumplist_info')
  Should o1 == o0 + 1
  Should n1 == n0 - 1
  Should bufnr('') == bufnr6

  It should go to the next buffer - usr_07.txt

  silent call exjumplist#next_buffer()
  let [o2, n2] = Call('s:jumplist_info')
  Should o2 == o1 + 2
  Should n2 == n1 - 2
  Should bufnr('') == bufnr7
endfunction




function s:describe__s_jumplist_info__at_first()  "{{{1
  " Set up jumplist - 0 / 3+
  silent help usr_01.txt
  normal! 10G
  normal! 100G
  let [o0, n0] = Call('s:jumplist_info')
  for i in range(o0)
    silent execute 'normal!' "\<C-o>"
  endfor

  It should return valid values

  let [o1, n1] = Call('s:jumplist_info')
  Should o1 == 0
  Should n1 >= 3
  Should o1 + n1 >= 3

  It should return valid values after <C-o>

    " No older position in jumplist - <C-o> does nothing.
  execute 'normal!' "\<C-o>"
  let [o2, n2] = Call('s:jumplist_info')
  Should o2 == o1
  Should n2 == n1

  It should return valid values after <C-i>

  silent execute 'normal' "\<Plug>(exjumplist-%-next-position)"
  let [o3, n3] = Call('s:jumplist_info')
  Should o3 - 1 == o2
  Should n3 + 1 == n2
endfunction




function s:describe__s_jumplist_info__at_last()  "{{{1
  " Set up jumplist - 3+ / 0
  silent help usr_02.txt
  normal! 10G
  normal! 100G

  It should return valid values

  let [o1, n1] = Call('s:jumplist_info')
  Should o1 >= 3
  Should n1 == 0
  Should o1 + n1 >= 3

  It should return valid values after <C-o>

  execute 'normal!' "\<C-o>"
  let [o2, n2] = Call('s:jumplist_info')
  Should o2 + 1 == o1
  Should n2 - 1 == n1

  It should return valid values after <C-i>

  execute 'normal' "\<Plug>(exjumplist-%-next-position)"
  let [o3, n3] = Call('s:jumplist_info')
  Should o3 - 1 == o2
  Should n3 + 1 == n2
  Should o3 == o1
  Should n3 == n1

  It should return valid values after one more <C-i>

    " No newer position in jumplist - <C-i> does nothing.
  execute 'normal' "\<Plug>(exjumplist-%-next-position)"
  let [o4, n4] = Call('s:jumplist_info')
  Should o4 == o3
  Should n4 == n3
  Should o4 == o1
  Should n4 == n1
endfunction




function s:describe__s_jumplist_info__at_middle()  "{{{1
  " Set up jumplist - 2+ / 1
  silent help
  normal! 10G
  normal! 100G
  execute 'normal!' "\<C-o>"

  It should return valid values

  let [o1, n1] = Call('s:jumplist_info')
  Should o1 >= 2
  Should n1 >= 1
  Should o1 + n1 >= 3

  It should return valid values after <C-o>

  execute 'normal!' "\<C-o>"
  let [o2, n2] = Call('s:jumplist_info')
  Should o2 + 1 == o1
  Should n2 - 1 == n1

  It should return valid values after <C-i>

  execute 'normal' "\<Plug>(exjumplist-%-next-position)"
  let [o3, n3] = Call('s:jumplist_info')
  Should o3 - 1 == o2
  Should n3 + 1 == n2
  Should o3 == o1
  Should n3 == n1
endfunction




" __END__  "{{{1
" vim: filetype=vim foldmethod=marker
